<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<ui:DraggablePanel xmlns="weave.example.*" xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:ui="weave.ui.*"
				   implements="weave.api.ui.IVisTool">
	<mx:Canvas width="100%" height="100%" id="canvas"/>
	<mx:Script>
		<![CDATA[
			import weave.api.WeaveAPI;
			import weave.api.data.IQualifiedKey;
			import weave.api.getCallbackCollection;
			import weave.api.newLinkableChild;
			import weave.api.ui.IVisTool;
			import weave.compiler.StandardLib;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.utils.ColumnUtils;
			WeaveAPI.registerImplementation(IVisTool, sp4);
			
			public const xData:DynamicColumn = newLinkableChild(this, DynamicColumn);
			public const yData:DynamicColumn = newLinkableChild(this, DynamicColumn);
			public const cData:DynamicColumn = newLinkableChild(this, DynamicColumn);
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				getCallbackCollection(this).addImmediateCallback(this, invalidateDisplayList);
			}
			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				super.updateDisplayList.apply(this, arguments);
				var keys:Array = ColumnUtils.getAllKeys([xData,yData,cData]);
				
				var yMin:Number = WeaveAPI.StatisticsCache.getMin(yData);
				var yMax:Number = WeaveAPI.StatisticsCache.getMax(yData);
				
				var xMin:Number = WeaveAPI.StatisticsCache.getMin(xData);
				var xMax:Number = WeaveAPI.StatisticsCache.getMax(xData);
				
				var cMin:Number = WeaveAPI.StatisticsCache.getMin(cData);
				var cMax:Number = WeaveAPI.StatisticsCache.getMax(cData);
				
				canvas.graphics.clear();
				for each (var key:IQualifiedKey in keys)
				{
					var x:Number = xData.getValueFromKey(key, Number);
					var y:Number = yData.getValueFromKey(key, Number);
					var c:Number = cData.getValueFromKey(key, Number);
					var r:Number = 2;
					
					var yNorm:Number = (y-yMin)/(yMax-yMin);
					var xNorm:Number = (x-xMin)/(xMax-xMin);
					var cNorm:Number = (c-cMin)/(cMax-cMin);
					
					var color:Number = StandardLib.interpolateColor(cNorm, 0xFF0000, 0x00FF00);
					
					canvas.graphics.beginFill(color, 1.0);
					canvas.graphics.drawCircle(xNorm*unscaledWidth, (1.0-yNorm)*unscaledHeight, r);
					canvas.graphics.endFill();
				}
			}
		]]>
	</mx:Script>
</ui:DraggablePanel>
